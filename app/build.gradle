buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'


    classpath 'com.github.triplet.gradle:play-publisher:1.2.2'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'spoon'
apply plugin: 'com.github.triplet.play'

play {
  track = 'beta'
  if (project.hasProperty("ANDROID_APPLI_RESEAU_SERVICE_ACCOUNT")) {
    serviceAccountEmail ANDROID_APPLI_RESEAU_SERVICE_ACCOUNT
  }
  pk12File = file('../key.p12')
}

android {
  compileSdkVersion compileSdkVer
  buildToolsVersion buildToolsVer

  defaultConfig {
    applicationId "com.demgird.applireseau"
    minSdkVersion minSdkVer
    targetSdkVersion targetSdkVer

    versionName "Presentation1.0"
    versionCode 21

    archivesBaseName = "AndroidAppliReseau"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  if (project.hasProperty("ANDROID_APPLI_RESEAU_STORE")) {
    signingConfigs {
      //from ~/.gradle/gradle.properties
      release {
        storeFile file(ANDROID_APPLI_RESEAU_STORE)
        keyAlias ANDROID_APPLI_RESEAU_ALIAS
        storePassword ANDROID_APPLI_RESEAU_PASS
        keyPassword ANDROID_APPLI_RESEAU_KEY_PASS
      }
    }
  }

  buildTypes {
    release {
      if (project.hasProperty("ANDROID_APPLI_RESEAU_STORE")) {
        signingConfig signingConfigs.release
      }
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

// Spoon used for CI / Testing
spoon {
  debug = true
  // To grant permissions to Android M >= devices */
  grantAllPermissions = true
}

dependencies {
  implementation "com.android.support:appcompat-v7:$supportLibVer"
  implementation "com.android.support:design:$supportLibVer"
  implementation project(':library')

  testImplementation 'junit:junit:4.12'

  androidTestImplementation 'com.squareup.spoon:spoon-client:1.6.4'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
  androidTestImplementation('com.android.support.test.espresso:espresso-intents:2.2')
}
